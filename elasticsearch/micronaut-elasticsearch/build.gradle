plugins {
    id "net.ltgt.apt-eclipse" version "0.21"
    id "com.github.johnrengelman.shadow" version "5.2.0"
    id "com.google.cloud.tools.jib" version "2.3.0"
    id "application"
}

version "1.0.0"
group "com.mycompany"

repositories {
    jcenter()
}

configurations {
    // for dependencies that are needed for development only
    developmentOnly
}

ext {
    set('micronautVersion', '2.0.0.M3')
    set('lombokVersion', '1.18.12')
    set('mapstructVersion', '1.3.1.Final')
    set('logbackVersion', '1.2.3')
    set('elasticSearchClientVersion', '7.6.2')
    set('apacheLog4jToSlf4jVersion', '2.13.3')
}

dependencies {
    //-- IMPORTANT!
    //  Lombok annotation processor must place before the Micronaut processors in the build configuration
    /// since Micronaut needs to see the mutations to the AST that Lombok applies.
    compileOnly("org.projectlombok:lombok:$lombokVersion")
    annotationProcessor("org.projectlombok:lombok:$lombokVersion")
    //--
    annotationProcessor(platform("io.micronaut:micronaut-bom:$micronautVersion")) // using platform instead of enforcedPlatform in order to set ES 7
    annotationProcessor("io.micronaut:micronaut-inject-java")
    annotationProcessor("io.micronaut:micronaut-validation")
    annotationProcessor("io.micronaut:micronaut-graal")
    compileOnly(platform("io.micronaut:micronaut-bom:$micronautVersion")) // using platform instead of enforcedPlatform in order to set ES 7
    compileOnly("org.graalvm.nativeimage:svm")
    implementation(platform("io.micronaut:micronaut-bom:$micronautVersion")) // using platform instead of enforcedPlatform in order to set ES 7
    implementation("io.micronaut:micronaut-inject")
    implementation("io.micronaut:micronaut-validation")
    implementation("io.micronaut:micronaut-runtime")
    implementation("io.micronaut:micronaut-http-server-netty")
    implementation("io.micronaut:micronaut-http-client")
    implementation("io.micronaut.cache:micronaut-cache-caffeine")
    implementation("io.micronaut:micronaut-management")
    implementation("io.micronaut.configuration:micronaut-micrometer-core")
    implementation("io.micronaut.configuration:micronaut-micrometer-registry-prometheus")
    implementation("io.micronaut.configuration:micronaut-elasticsearch")
    implementation("org.elasticsearch.client:elasticsearch-rest-high-level-client:$elasticSearchClientVersion")
    //-- LOG4J-to-SLF4J
    // It removes an error messages that says:
    // ERROR StatusLogger Log4j2 could not find a logging implementation. Please add log4j-core to the classpath. Using SimpleLogger to log to the console...
    implementation("org.apache.logging.log4j:log4j-to-slf4j:$apacheLog4jToSlf4jVersion")
    //--
    compileOnly("org.mapstruct:mapstruct-jdk8:$mapstructVersion")
    annotationProcessor("org.mapstruct:mapstruct-processor:$mapstructVersion")
    runtimeOnly("ch.qos.logback:logback-classic:$logbackVersion")
    testAnnotationProcessor(enforcedPlatform("io.micronaut:micronaut-bom:$micronautVersion"))
    testAnnotationProcessor("io.micronaut:micronaut-inject-java")
    testImplementation(enforcedPlatform("io.micronaut:micronaut-bom:$micronautVersion"))
    testImplementation("org.junit.jupiter:junit-jupiter-api")
    testImplementation("io.micronaut.test:micronaut-test-junit5")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
}

test.classpath += configurations.developmentOnly

mainClassName = "com.mycompany.Application"

// use JUnit 5 platform
test {
    useJUnitPlatform()
}

java {
    sourceCompatibility = JavaVersion.toVersion('11')
    targetCompatibility = JavaVersion.toVersion('11')
}

tasks.withType(JavaCompile){
    options.encoding = "UTF-8"
    options.compilerArgs.add('-parameters')
}

shadowJar {
    mergeServiceFiles()
}

tasks.withType(JavaExec) {
    classpath += configurations.developmentOnly
    jvmArgs('-XX:TieredStopAtLevel=1', '-Dcom.sun.management.jmxremote')
}

// This value is overwritten in docker-build.sh
jib.to.image = "docker.mycompany.com/micronaut-elasticsearch:${project.version}"
